/* ---------------Seccion de declaraciones preliminares--------------------*/
package parser.data;

/* Import the class java_cup.runtime.*  */
import java_cup.runtime.*;
import java.io.FileReader;
import java.util.Hashtable;
import java.util.Stack;
import parser.controller.DatosArchivoController;
   
/* Codigo del parser, se copia integramente a la clase final.
    Agregamos el manejo de errores. */
parser code {:

    public static Lexer s;
    public static int errors = 0;
    public static ErrorData errorData = ErrorData.getInstance();
    public DatosArchivoController archivoController = DatosArchivoController.getInstance();
    ErrorData detectedError;
    public TablaSimbolos tablaSimbolos = new TablaSimbolos();
    public PilaSemantica pilaSemantica = new PilaSemantica();
    String assemblyFile = "";
    
    /* Reporte de error encontrado. */

    public void report_error(String message,int line, int column) {
        StringBuilder m = new StringBuilder("Error");
        m.append(" en la línea "+(line+1));
        m.append(", columna "+(column+1));
        m.append(" : " + message);
        archivoController.mostrarErroresSemanticos(m.toString());
    }
    public void report_error(String message, Object info) {
        if (info instanceof String){
            errors++;
            String mensaje = ", se esperaba: " + message;
            try{
                detectedError.setId(errors);
            }catch(Exception e){
                detectedError = new ErrorData(errors, info.toString(), mensaje);
            }
            detectedError.setMessage(detectedError.getMessage() + mensaje);
            errorData.insertarError(detectedError);
        }
        else {
            StringBuilder m = new StringBuilder("Error");
            if (info instanceof java_cup.runtime.Symbol) {
                java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
                if (s.left >= 0) {                
                    m.append(" en la línea "+(s.left+1));
                    if (s.right >= 0)
                        m.append(", columna "+(s.right+1));
                }
            }
            m.append(" : " + message);
            detectedError = new ErrorData(errors, info.toString(), m.toString());
        }
    }
   
    /* Cuando se encuentra un error de donde el sistema no puede
        recuperarse, se lanza un error fatal. Se despliega el mensaje
        de error y se finaliza la ejecucion. */
    public void report_fatal_error(String message, Object info) {
        System.out.println("----------FUI YO------------");
        report_error(message, info);
        System.exit(1);
    }

    /* Metodo main para garantizar la ejecucion del analizador
       lexico y sintactico, ademas que se pase como parametro la tabla
       de simbolos correspondiente. */
    public static void main(String[] args){
        try {
            s = new Lexer(new FileReader(args[0]));
            Parser p = new Parser(s);
            p.parse();
            System.out.println("\n*** Fin del parseo ***");
            System.out.println("Total de errores = " + errors + ".");
            errors = 0;
        } catch (Exception ex) {
            ex.printStackTrace();
        }
    }
:};
   

   
/* ------------Declaracion de simbolos terminales y no terminales---------- */

/* OPERADORES */
terminal            ADD, SUBSTRACT, TIMES, DIVISION, MODULUS;
terminal            PLUSPLUS, MINUSMINUS;
terminal            ASSIGN, PLUSEQ, MINUSEQ, TIMESEQ, DIVEQ, MODEQ;

/* SEPARADORES */
terminal            LPAREN, RPAREN, LBRACE, RBRACE, SEMICOLON, COLON, COMMA;

/* SENTENCIAS */
terminal            IF, ELSE, WHILE;

/* COMPARADORES */
terminal            EQUAL, MENORQUE,MENOREQ, MAYORQUE, MAYOREQ, EXCLAMACION, NOTEQ, OR, AND;

/* PALABRAS RESERVADAS */
terminal            RETURN, VOID;

/* FUNCIONES */
terminal            READ,WRITE;

/* TIPOS */
terminal            INT;
terminal Integer    LITERAL_ENTERO;
terminal            LITERAL_CARACTER;

/* IDENTIFICADOR */
terminal            IDENTIFICADOR;

/* ERROR*/
terminal            ERROR;

non terminal            inicio, programa_c , def_funcion , funcion;
non terminal            read_f,write_f, write_f_aux;
non terminal            bloque_global, bloque, lista_sentencias, sentencia, expresion;
non terminal            asignacion, exp, op_asignaciones, call_function;
non terminal            if_statement, while_statement, else_statement, return_stmt;
non terminal            condicion, valor;
non terminal            decision_funcion, decision_operacion, validar_expr, declaracion_var_interna;
non terminal            operacion_bin_1, operador, comparador, condicion_2;
non terminal            literal, fin_archivo, declaration_part, function_declaration_part, id_declaration, verification_part;
non terminal            condicion_stmt, test_if, start_if, else_stmt, variable;

precedence left OR, AND;
precedence left ADD, SUBSTRACT;
precedence left TIMES, DIVISION, MODULUS;

/* ------------------- Seccion de la gramatica ------------------------ */
    start with inicio;

    inicio              ::= programa_c;

    programa_c          ::=   variable  programa_c 
                            | function_declaration_part id_declaration decision_operacion:s 
                                {:
                                    RegistroSemantico RS_ID = parser.pilaSemantica.pop();
                                    RegistroSemantico RS_TIPO = parser.pilaSemantica.pop();
                                    if(parser.tablaSimbolos.get(RS_ID.getToken()) == null){                                    
                                        parser.tablaSimbolos.put(RS_ID.getToken(), 
                                                                 new InfoKey("Función", 
                                                                              RS_TIPO.getToken())); 
                                        System.out.println("DATOS TABLA SIMBOLOS FUNCION");
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getValor());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getTipo());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getCategoria());
                                    }else{
                                        if(parser.tablaSimbolos.get(RS_ID.getToken()).getNumDeclarado() <= 1){
                                            parser.tablaSimbolos.get(RS_ID.getToken()).sumarDeclarado();
                                            parser.report_error(" identificador '" + RS_ID.getToken() + "' ya ha sido declarado.", sleft,sright); 
                                        }
                                    }
                                :} programa_c 
                            | declaration_part id_declaration decision_operacion:s
                                {:
                                    RegistroSemantico RS_ID = parser.pilaSemantica.pop();
                                    System.out.println("------------------"+RS_ID.getToken());
                                    RegistroSemantico RS_TIPO = parser.pilaSemantica.pop();
                                    if(parser.tablaSimbolos.get(RS_ID.getToken()) == null){                                    
                                        parser.tablaSimbolos.put(RS_ID.getToken(), 
                                                                 new InfoKey("Función", 
                                                                              RS_TIPO.getToken())); 
                                        System.out.println("DATOS TABLA SIMBOLOS FUNCION INT");
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getValor());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getTipo());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(RS_ID.getToken())).getCategoria());
                                    }else{
                                        System.out.println("-------------NO______________");
                                        if(parser.tablaSimbolos.get(RS_ID.getToken()).getNumDeclarado() <= 1){
                                            parser.tablaSimbolos.get(RS_ID.getToken()).sumarDeclarado();
                                            parser.report_error(" identificador '" + RS_ID.getToken() + "' ya ha sido declarado.", sleft,sright); 
                                        }
                                    }
                                :} programa_c 
                            |;

    variable                    ::= declaration_part id_declaration verification_part SEMICOLON:s
                                {:  
                                    RegistroSemantico RS_DO = parser.pilaSemantica.pop();
                                    String id,valor;
                                    if(RS_DO.getTipo()!="RS_ID"){
                                        RegistroSemantico RS_ID = parser.pilaSemantica.pop();
                                        System.out.println("::::::::::"+RS_ID.getToken());
                                        id=RS_ID.getToken();
                                        valor= RS_DO.getValor();
                                    }else{
                                        id=RS_DO.getToken();
                                        valor="0";
                                    }
                                    RegistroSemantico RS_TIPO = parser.pilaSemantica.pop();
                                    
                                    if(parser.tablaSimbolos.get(id) == null){
                                        parser.tablaSimbolos.put(id, 
                                                                 new InfoKey("Identificador", 
                                                                              RS_TIPO.getToken(), 
                                                                              valor)); 
                                        System.out.println("DATOS TABLA SIMBOLOS");
                                        System.out.println(id);
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(id)).getValor());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(id)).getTipo());
                                        System.out.println(((InfoKey) parser.tablaSimbolos.get(id)).getCategoria());    
                                    }else{
                                        if(parser.tablaSimbolos.get(id).getNumDeclarado() <= 1){
                                            parser.tablaSimbolos.get(id).sumarDeclarado();
                                            parser.report_error(" identificador '" + id + "' ya ha sido declarado 1.", sleft,sright); 
                                        }
                                    }
                                :};

    declaration_part            ::= INT:tipo {: 
                                        parser.pilaSemantica.push(new RegistroSemantico(tipo.toString(), "RS_TIPO"));//Crear RS_Tipo
                                        RESULT = tipo;
                                    :};
    
    function_declaration_part   ::= VOID:tipo {: 
                                        parser.pilaSemantica.push(new RegistroSemantico(tipo.toString(), "RS_TIPO"));//Crear RS_Tipo
                                        RESULT = tipo;
                                    :};

    id_declaration              ::= IDENTIFICADOR:id {: 
                                        parser.pilaSemantica.push(new RegistroSemantico(id.toString(), "RS_ID"));//Crear RS_Tipo
                                        RESULT = id;
                                    :};

    verification_part           ::= validar_expr:exp;

    decision_operacion   ::= def_funcion | funcion;

    def_funcion         ::= LPAREN RPAREN SEMICOLON;
    
    funcion             ::= LPAREN RPAREN bloque_global | 
                            LPAREN error {: parser.report_error(" parámetro incorrecto.",""); :} RPAREN bloque_global;
    
    bloque_global       ::= LBRACE declaracion_var_interna lista_sentencias RBRACE 
                            | LBRACE RBRACE
                            | error {: parser.report_error(" no se encuentra el LBRACE ACA .","");  :};
    
    bloque              ::= LBRACE lista_sentencias RBRACE | LBRACE RBRACE | 
                            error {: parser.report_error(" no se encuentra el LBRACE o expresión inválida.","");  :} decision_funcion;

    declaracion_var_interna     ::= variable declaracion_var_interna | ;

    lista_sentencias    ::= lista_sentencias sentencia | sentencia ;

    sentencia           ::= if_statement |  
                            while_statement | 
                            expresion SEMICOLON | 
                            return_stmt ;
 
    expresion           ::=  exp:exp1 {: RESULT = exp1; :} | EXCLAMACION exp | call_function | asignacion;
    
    asignacion          ::= IDENTIFICADOR op_asignaciones exp;

    op_asignaciones     ::= ASSIGN ;

    exp                 ::= valor operador expresion 
                            {: RegistroSemantico RS_DO1= parser.pilaSemantica.pop();
                               RegistroSemantico RS_OP= parser.pilaSemantica.pop();
                               RegistroSemantico RS_DO2= parser.pilaSemantica.pop();
                               IOperacion op = (IOperacion)new OperacionFactory(RS_OP.getTipo()).getOp();
                               op.setOperandos(RS_DO1,RS_DO2);
                               op.generarCodigo();
                               //Se crea una variable donde guardar Resultado
                            :}|
                            valor comparador expresion  
                            {: RegistroSemantico RS_DO1= parser.pilaSemantica.pop();
                               RegistroSemantico RS_OP= parser.pilaSemantica.pop();
                               RegistroSemantico RS_DO2= parser.pilaSemantica.pop();
                               IOperacion op = (IOperacion)new OperacionFactory(RS_OP.getTipo()).getOp();
                               op.setOperandos(RS_DO1,RS_DO2);
                               op.generarCodigo();
                               //Se crea una variable donde guardar Resultado
                            :}|
                            LPAREN expresion RPAREN |
                            valor operacion_bin_1  
                            {: RegistroSemantico RS_DO1= parser.pilaSemantica.pop();
                               RegistroSemantico RS_OP= parser.pilaSemantica.pop();
                               IOperacion op = (IOperacion)new OperacionFactory(RS_OP.getTipo()).getOp();
                               op.setOperandor(RS_DO1);
                               op.generarCodigo();
                               //Se crea una variable donde guardar Resultado
                            :}|
                            valor;

    call_function       ::= IDENTIFICADOR LPAREN RPAREN | read_f | write_f ;
            
    condicion           ::= condicion_2 AND condicion | 
                            condicion_2 OR condicion | 
                            condicion_2:exp {: RESULT = exp; :};

    condicion_2         ::= expresion:exp {: RESULT = exp; :};

    read_f              ::= READ LPAREN IDENTIFICADOR RPAREN | READ LPAREN RPAREN | 
                            READ LPAREN error{: parser.report_error(" parámetros incorrectos en el READ.", ""); :} RPAREN;

    write_f             ::= WRITE LPAREN write_f_aux RPAREN |
                            WRITE LPAREN error{: parser.report_error(" parámetros incorrectos en el WRITE.", ""); :} RPAREN;

    write_f_aux         ::= write_f_aux COMMA expresion | expresion; 
        
    if_statement        ::= start_if LPAREN condicion_stmt test_if bloque else_statement 
                                    {: 
                                        parser.assemblyFile += "exit_label: \n";
                                        System.out.println("IF: " + parser.assemblyFile);
                                        parser.pilaSemantica.pop();
                                    :};
                            /*IF LPAREN 
                                error {: parser.report_error(" una condición válida para el IF.", ""); :} 
                            RPAREN bloque else_statement |
                            IF LPAREN
                                    error {: parser.report_error(" un RPAREN al final de la condición del IF.", ""); :} 
                            bloque| 
                            IF  
                                error {: parser.report_error(" un LPAREN al final de la condición del IF.", ""); :} 
                            RPAREN bloque else_statement;*/

    start_if            ::= IF:tipo {:
                                    RegistroSemanticoSTMT RS_IF = new RegistroSemanticoSTMT(tipo.toString(), "RS_IF");//Crear RS_IF
                                    RS_IF.setStmt_label(parser.tablaSimbolos.getLabelName());
                                    RS_IF.setExit_label(parser.tablaSimbolos.getLabelName());
                                    parser.pilaSemantica.push(RS_IF);
                                :};

    condicion_stmt      ::= condicion:exp {:
                                    parser.pilaSemantica.push(new RegistroSemantico(exp.toString(), "RS_DO"));//Crear RS_DO
                                :};

    test_if             ::= RPAREN {:
                                    RegistroSemantico RS_DO = parser.pilaSemantica.pop();
                                    System.out.println("TEST_IF: " + RS_DO.getToken());
                                    parser.assemblyFile+= "cmp eax, ebx \n";
                                    parser.assemblyFile+= "JNZ "+ ((RegistroSemanticoSTMT)parser.pilaSemantica.searchRS("RS_IF")).getExit_label() +  "\n" ;
                                :};

    else_statement      ::= else_stmt bloque |;

    else_stmt           ::= ELSE {:
                                     parser.assemblyFile += "JMP exit_label \n";
                                     parser.assemblyFile += "else_label: \n";
                                 :};

    while_statement     ::= WHILE LPAREN condicion RPAREN bloque|
                            WHILE LPAREN
                                error {: parser.report_error("una condición valida para el WHILE.", ""); :} 
                            RPAREN bloque|
                            WHILE LPAREN
                                error {: parser.report_error(" un RPAREN al final de la condición del WHILE.", ""); :} 
                            bloque
                            | WHILE 
                                error {: parser.report_error(" un LPAREN al final de la condición del WHILE.", ""); :} 
                            RPAREN bloque;
    
    return_stmt         ::= RETURN expresion:e1 {: RESULT = e1; :} SEMICOLON;

    validar_expr        ::= ASSIGN expresion:exp {: RESULT = exp; :}
                            | ASSIGN error {: parser.report_error(" una expresión o falta ; al final.",""); :}
                            | {: RESULT = ""; :};

    comparador          ::= EQUAL:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"Igual");
                               parser.pilaSemantica.push(RS_OP);
                            :}| NOTEQ:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"NoIgual");
                               parser.pilaSemantica.push(RS_OP);
                            :}| MENOREQ:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"MenorIgual");
                               parser.pilaSemantica.push(RS_OP);
                            :}| MAYOREQ:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"MayorIgual");
                               parser.pilaSemantica.push(RS_OP);
                            :}| MENORQUE:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"Menor");
                               parser.pilaSemantica.push(RS_OP);
                            :}| MAYORQUE:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"Mayor");
                               parser.pilaSemantica.push(RS_OP);
                            :};

    operador            ::= ADD:a 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(a.toString(),"Suma");
                               parser.pilaSemantica.push(RS_OP);
                            :}| 
                            SUBSTRACT:s  
                            {: RegistroSemantico RS_OP = new RegistroSemantico(s.toString(),"Resta");
                               parser.pilaSemantica.push(RS_OP);
                            :};

    operacion_bin_1     ::= PLUSPLUS:op 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(op.toString(),"MasMas");
                               parser.pilaSemantica.push(RS_OP);
                            :}| MINUSMINUS:p 
                            {: RegistroSemantico RS_OP = new RegistroSemantico(p.toString(),"MenosMenos");
                               parser.pilaSemantica.push(RS_OP);
                            :};

    valor               ::= IDENTIFICADOR:i 
                            {:  RegistroSemantico RS_DO = new RegistroSemantico(i.toString(),"RS_DO_D");
                                if( parser.tablaSimbolos.get(RS_DO.getToken()) ==null){
                                    //CODIGO ERROR
                                }
                                parser.pilaSemantica.push(RS_DO);
                            :} 
                            | LITERAL_ENTERO:lit 
                            {:  System.out.println("---------------CREADO----------------");
                                RegistroSemantico RS_DO = new RegistroSemantico(lit.toString(),"RS_DO_C",lit.toString());
                                parser.pilaSemantica.push(RS_DO);
                            :} 
                            | SUBSTRACT LITERAL_ENTERO:lit {: RESULT = lit; :};
